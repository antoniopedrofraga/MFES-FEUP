class Feature
types
			public string = Utilities`string;
			public config = Utilities`config;
instance variables
			public name: string;
			protected mandatory: bool := true;
			protected requirements: seq of Feature := [];
			protected exclusions: seq of Feature := [];
operations
			public Feature: string ==> Feature
							Feature(n) == (name := n; return self;)
							pre not n = ""
							post name = n;
			public setMandatory: bool ==> ()
							setMandatory(b) == mandatory := b;
			public setRequirements: seq of Feature ==> ()
							setRequirements(fs) == (requirements := fs;)
							post elems requirements inter elems exclusions = {};
			public setExclusions: seq of Feature ==> ()
							setExclusions(fs) == exclusions := fs
							post {} = {element | element in seq exclusions & element.mandatory} and elems requirements inter elems exclusions = {};
			public getRequirements: () ==> seq of Feature
						getRequirements() == return requirements;
			public getExclusions: () ==> seq of Feature
						getExclusions() == return exclusions;
			public getName: () ==> string
							getName() == return name;
			public features: () ==> set of string
							features() == return {name};
			public invalidSubsets: () ==> seq of config
							invalidSubsets() == 
							(
							dcl restrictions : seq of config := [];
							if (len requirements + len exclusions > 0) then
							(
									for requirement in requirements do restrictions := restrictions ^ [{name |-> true} munion {requirement.getName() |-> false}];
									for exclusion in exclusions do restrictions := restrictions ^ [{name |-> true} munion {exclusion.getName() |-> true}];
							);
							return restrictions;
			);
			public nodeCount: () ==> int
							nodeCount() == return 1;
			public isMandatory: () ==> bool
							isMandatory() == return mandatory;
			public isRespectingReqAndExc: config ==> bool
							isRespectingReqAndExc(c) == 
							(
							dcl configuredFeatures : set of string := dom (c :> {true});
							for requirement in requirements do 
									if not (requirement.name in set configuredFeatures) then return false;
							for exclusion in exclusions do 
									if (exclusion.name in set configuredFeatures) then return false;
							return true;
							);
			public isValidConfiguration: config ==> bool
							isValidConfiguration(c) == 
							(
							if mandatory and not Utilities`isNameConfigured(name, c) then return false;
							return isRespectingReqAndExc(c);
							);
end Feature